buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url uri('../repo')
        }
    }
    dependencies {
        classpath 'com.github.ksoichiro:gradle-web-resource-plugin:+'
        classpath 'net.rdrei.android.buildtimetracker:gradle-plugin:0.5.0'
    }
}

// If you use the released version, use plugins closure and
// comment out buildscript closure and apply plugin statement.
//plugins {
//    id "com.github.ksoichiro.web.resource" version "1.1.2"
//}

apply plugin: 'java'
apply plugin: 'com.github.ksoichiro.web.resource'
apply plugin: 'build-time-tracker'

webResource {
    less {
        logLevel LogLevel.INFO
    }

    coffeeScript {
        logLevel LogLevel.INFO
    }

    // You can also set like this:
    //coffeeScript.minify true

    bower {
        dependencies {
            // If the dependency has some dependencies, write them to upper.
            // Otherwise, the versions cannot be resolved and it might cause build errors.
            // For example, bootstrap has dependency for jquery, so jquery must be written BEFORE bootstrap.
            // If you write jquery AFTER bootstrap, installation of bootstrap will also install jquery,
            // but after that, jquery installation by 'install name:"jquery"...' will fail.
            install name: "jquery", version: "1.11.2", filter: ["dist/*.min.*"]
            install name: "chartjs", version: "1.0.2", cacheName: "Chart.js", filter: ["*.min.*"]
            install name: "bootstrap", version: "3.3.4", filter: ["dist/css/*.min.css", "dist/js/*.min.js", "dist/fonts/*"]
            install name: "respond-minmax", version: "1.4.2", cacheName: "respond", filter: ["dest/*.min.js"]
            install name: "html5shiv", version: "3.7.2", filter: ["dist/*.min.js"]
            install name: "jquery.slimscroll", version: "1.3.6", cacheName: "slimScroll", filter: ["*.js"]
            install name: "components-font-awesome", version: "4.3.0", outputName: "font-awesome"
        }
    }
}

buildtimetracker {
    reporters {
        summary {
            ordered true
            threshold 50
            barstyle "unicode"
        }
    }
}
